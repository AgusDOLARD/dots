#!/usr/bin/zsh
if (( $+commands[pacman] )); then
	command_not_found_handler() {
		local pkgs cmd="$1" files=()
		printf 'zsh: command not found: %s' "$cmd" # print command not found asap, then search for packages
		files=(${(f)"$(pacman -F --machinereadable -- "/usr/bin/${cmd}")"})
		if (( ${#files[@]} )); then
			printf '\r%s may be found in the following packages:\n' "$cmd"
			local res=() repo package version file
			for file in "$files[@]"; do
				res=("${(0)file}")
				repo="$res[1]"
				package="$res[2]"
				version="$res[3]"
				file="$res[4]"
				printf '  %s/%s %s: /%s\n' "$repo" "$package" "$version" "$file"
			done
		else
			printf '\n'
		fi
		return 127
	}
fi
